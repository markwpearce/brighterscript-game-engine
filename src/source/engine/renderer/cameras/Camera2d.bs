namespace BGE

  class Camera2d extends Camera


    protected top as float
    protected bottom as float
    protected right as float
    protected left as float

    protected near as float = 1000
    protected far as float = -10000

    'protected orthoProj as BGE.Math.Matrix44

    override sub setTarget(targetPos as BGE.Math.Vector)
      ' moves camera to center on target
      m.position.x = targetPos.x
      m.position.y = targetPos.y
    end sub

    override sub rotate(rotation as BGE.Math.Vector)
      ' 2d Cameras don't rotate
      ' TODO: The probably could rotate in in Z axis. hmmm
    end sub

    override function isInView(point as BGE.Math.Vector) as boolean
      ' add a bit of fudge value here, so things don't disappear when still in screen
      xFudge = m.frameSize.x / 4
      yFudge = m.frameSize.y / 4

      inViewHorizontally = point.x >= (m.left - xFudge) and point.x <= (m.right + xFudge)
      inViewHorizontally = point.y >= (m.top - yFudge) and point.y <= (m.bottom + yFudge)

      return inViewHorizontally and inViewHorizontally
    end function

    override sub onCameraMovement()

      halfW = m.frameSize.x / 2
      halfH = m.frameSize.y / 2

      m.setTarget(new BGE.Math.Vector(halfW, HalfH))

    end sub


    override sub computeWorldToCameraMatrix()

      halfW = m.frameSize.x / 2
      halfH = m.frameSize.y / 2

      m.top = m.position.y + halfH
      m.bottom = m.position.y - halfH
      m.right = m.position.x + halfW
      m.left = m.position.x - halfW
      m.near = m.position.z + 1
      m.far = -10000

      m.worldToCamera = BGE.Math.orthographicMatrix(m.top, m.bottom, m.left, m.right, m.far, m.near)
    end sub


    override function distanceFromCameraFront(point as BGE.Math.Vector) as float
      return m.position.z - point.z
    end function

    override function worldPointToCanvasPoint(pWorld as BGE.Math.Vector) as BGE.Math.Vector
      if invalid = m.worldToCamera
        m.computeWorldToCameraMatrix()
      end if

      'pCamera = m.orthoProj.multVecMatrix(m.worldToCamera.multVecMatrix(pWorld))
      pCamera = m.worldToCamera.multVecMatrix(pWorld)
      if pCamera.z >= 0
        return invalid
      end if

      canvasWidth = m.frameSize.x
      canvasHeight = m.frameSize.y
      pScreen = {x: 0, y: 0}
      pScreen.x = pCamera.x * canvasWidth * 0.5
      pScreen.y = pCamera.y * canvasHeight * 0.5
      pNDC = {x: 0, y: 0}

      pNDC.x = (pScreen.x + canvasWidth * 0.5) '/ canvasWidth
      pNDC.y = (-pScreen.y + canvasHeight * 0.5) '/ canvasHeight
      pRaster = new BGE.Math.Vector()

      pRaster.x = fix(pNDC.x)
      pRaster.y = fix (pNDC.y)
      '? "pWorld: "; pWorld.toStr();" -> screen: [";pScreen.x;pScreen.y;"] -> NDC [";pNDC.x;pNDC.y;"] -> Raster [";pRaster.x;pRaster.y;"]"
      return pRaster
    end function


  end class


end namespace