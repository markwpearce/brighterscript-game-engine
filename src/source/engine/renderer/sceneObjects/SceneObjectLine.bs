namespace BGE

  class SceneObjectLine extends SceneObject

    drawable as DrawableLine

    private worldStart as BGE.Math.Vector
    private worldEnd as BGE.Math.Vector
    private canvasStart as BGE.Math.Vector
    private canvasEnd as BGE.Math.Vector

    sub new(name as string, drawableObj as DrawableLine)
      super(name, drawableObj, BGE.SceneObjectType.Line)

    end sub

    override sub draw(renderer as BGE.Renderer)
      didDraw = false
      if m.isPotentiallyOnScreen(renderer.camera)
        if m.drawable.movedLastFrame(true) or invalid = m.worldStart or invalid = m.worldEnd
          '? "Finding New World points - ";m.name
          m.worldStart = m.transformationMatrix.multVecMatrix(m.drawable.startPosition)
          m.worldEnd = m.transformationMatrix.multVecMatrix(m.drawable.endPosition)
        end if
        if renderer.camera.movedLastFrame() or m.drawable.movedLastFrame(true)
          somethingMoved = true
          '? "Finding New Canvas points - ";m.name
          m.canvasStart = renderer.worldPointToCanvasPoint(m.worldStart)
          m.canvasEnd = renderer.worldPointToCanvasPoint(m.worldEnd)
        end if
        if m.canvasStart = invalid or m.canvasEnd = invalid
          didDraw = false
        else

          didDraw = renderer.drawLine(m.canvasStart.x, m.canvasStart.y, m.canvasEnd.x, m.canvasEnd.y, m.drawable.getFillColorRGBA())
        end if
      end if
      if didDraw
        m.resetFrameSinceDrawn()
      else
        m.framesSinceDrawn++
      end if
    end sub

  end class

end namespace