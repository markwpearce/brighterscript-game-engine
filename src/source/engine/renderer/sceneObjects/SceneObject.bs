namespace BGE

  enum SceneObjectType
    Line = "Line"
    Rectangle = "Rectangle"
    Text = "Text"
    Bitmap = "Bitmap"
    Polygon = "Polygon"
  end enum

  class SceneObject

    name as string

    ' Unique Id
    id as string = ""

    drawable as Drawable

    type as SceneObjectType

    normDistanceFromCamera as float = 0

    worldPosition as BGE.Math.Vector = new BGE.Math.Vector()

    ' The Current Transformation Matrix
    transformationMatrix as BGE.Math.Matrix44 = new BGE.Math.Matrix44()

    lastBitmap as object

    sub new(name as string, drawableObj as Drawable, objType as SceneObjectType)
      m.name = name
      m.drawable = drawableObj
      m.type = objType
    end sub

    sub setId(id as string)
      if m.id = ""
        m.id = id
      end if
    end sub

    sub update(cameraPosition as BGE.Math.Vector)
      m.worldPosition = m.drawable.getWorldPosition()
      m.normDistanceFromCamera = m.worldPosition.subtract(cameraPosition).norm()

      drawableMat = BGE.Math.getTransformationMatrix(m.drawable.offset, m.drawable.rotation, m.drawable.scale)
      'm.transformationMatrix.setFrom(drawableMat)
      m.transformationMatrix.setFrom(m.drawable.owner.transformationMatrix.multiply(drawableMat))
    end sub

    function enabled() as boolean
      return m.drawable.enabled
    end function


    sub draw(renderer as Renderer)
      ' No op - needs to be overriden in Specific types of scene objects
    end sub

  end class




end namespace