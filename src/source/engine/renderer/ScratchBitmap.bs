namespace BGE

  class ScratchBitmap

    bitmap as roBitmap
    id as string

    sub new(id as string, w as integer, h as integer)
      m.bitmap = CreateObject("roBitmap", {width: w, height: h, alphaEnable: true})
      m.id = id
    end sub
  end class


  class ScratchRegion

    region as roRegion
    scratchBmp as ScratchBitmap

    sub new(region as roRegion, scratchBmp as ScratchBitmap)
      m.region = region
      m.scratchBmp = scratchBmp
    end sub

    function getBitmapObject() as roBitmap
      return m.scratchBmp.bitmap
    end function

  end class

  class ScratchBitmapPool

    private pool as object[] = []
    private scratchHeight as integer = 1080
    private scratchWidth as integer = 1920

    private nextId = 0

    private maxPoolCount = 10
    private activePoolCount = 0

    private doPooling = false

    sub new(initialCount = 0)
      for i = 0 to initialCount - 1
        m.addNewBitmapToPool()
      end for
    end sub


    private sub addNewBitmapToPool(w = -1, h = -1)
      id = m.nextId.toStr()
      m.nextId++

      m.pool.push(new ScratchBitmap(id, m.scratchWidth, m.scratchHeight))
      m.activePoolCount++
    end sub


    function getRegion(width as float, height as float, onTopRight = false) as ScratchRegion
      if not m.doPooling
        return getScratchRegion(width, height)
      end if
      width = BGE.Math.Clamp(fix(width + 1), 256, m.scratchWidth)
      height = BGE.Math.Clamp(fix(height + 1), 256, m.scratchHeight)

      if width > m.scratchWidth or height > m.scratchHeight
        return invalid
      end if
      bmp = invalid
      if m.pool.count() > 0
        bmp = m.pool.pop()
      end if
      if bmp = invalid and m.activePoolCount < m.maxPoolCount
        m.addNewBitmapToPool()
        bmp = m.pool.pop()
        if bmp = invalid or bmp.bitmap = invalid
          return invalid
        end if
      end if

      region = invalid
      if bmp = invalid
        return invalid
      end if
      if not onTopRight
        ' create region on top left
        region = CreateObject("roRegion", bmp.bitmap, 0, 0, width, height)
      else
        ' create region on top right
        region = CreateObject("roRegion", bmp.bitmap, m.scratchWidth - width, 0, width, height)
      end if
      if invalid = region
        m.pool.push(bmp)
        return invalid
      end if

      bmp.bitmap.clear(0)
      return new ScratchRegion(region, bmp)
    end function

    sub returnRegion(usedRegion as ScratchRegion)
      if usedRegion = invalid or not m.doPooling
        return
      end if

      inProgBmp = usedRegion.scratchBmp
      ' inProgBmp.bitmap.finish()
      m.pool.push(inProgBmp)
    end sub

    sub returnRegions(usedRegions as ScratchRegion[])
      if not m.doPooling
        return
      end if

      for i = usedRegions.count() - 1 to 0 step -1
        region = usedRegions[i]
        m.returnRegion(region)
      end for
      if m.pool.Count() < 5
        print "uh oh... pool drained"
      end if
    end sub

    sub clearPool()
      m.pool = []
      m.activePoolCount = 0
    end sub

  end class




  function getScratchRegion(width as float, height as float) as ScratchRegion
    if width < 1 or height < 1
      return invalid
    end if
    bmp = new ScratchBitmap("temp", width, height)
    if invalid = bmp
      return invalid
    end if
    region = CreateObject("roRegion", bmp.bitmap, 0, 0, width, height)
    return new ScratchRegion(region, bmp)
  end function

end namespace