' @module BGE
namespace BGE

  ' Used to draw a bitmap image to the screen
  class Image extends BGE.Drawable

    ' -------------Never To Be Manually Changed-----------------
    ' These values should never need to be manually changed.
    public region as object


    sub new(owner as BGE.GameEntity, canvasBitmap as object, region as object, args = {} as object)
      super(owner, canvasBitmap, args)
      m.region = region
      if m.region <> invalid
        m.width = m.region.getWidth()
        m.height = m.region.getHeight()
      end if
      ' bs:disable-next-line: 1001
      m.append(args)
    end sub

    override function getPretranslation() as BGE.Math.Vector
      return BGE.Math.VectorOps.create(m.region.GetPretranslationX(), m.region.GetPretranslationY())
    end function


    override sub draw(additionalRotation = invalid as BGE.Math.Vector)
      m.width = m.region.getWidth()
      m.height = m.region.getHeight()
      if m.enabled
        m.drawRegionToCanvas(m.region, additionalRotation)
      end if
    end sub

    override sub addToScene(rendererObj as Renderer)
      m.addSceneObjectToRenderer(new BGE.SceneObjectImage(m.getSceneObjectName("image"), m as Image), rendererObj)
    end sub


  end class
end namespace
