' @module BGE
namespace BGE

  ' Class to draw text
  class DrawableText extends Drawable

    ' The text to write on the screen
    text as string = ""
    ' The Font object to use ( get this from the font registry)
    font as object

    ' The Horizontal alignment for the text
    alignment as BGE.UI.HorizAlignment = BGE.UI.HorizAlignment.left

    protected lastTextValue as string = ""
    protected tempCanvas as object = invalid

    function new(owner as GameEntity, canvasBitmap as object, text = "" as string, font = invalid as object, args = {} as object) as void
      super(owner, canvasBitmap, args)
      m.text = text
      m.font = font
      if invalid <> m.owner and invalid <> m.owner.game and invalid = m.font
        m.font = m.owner.game.getFont("default")
      end if
      m.append(args)
    end function

    override function draw(additionalRotation = invalid as BGE.Math.Vector) as void
      if not m.enabled
        return
      end if

      if m.text = m.lastTextValue and invalid <> m.tempCanvas and not m.shouldRedraw
        m.drawRegionToCanvas(m.tempCanvas, additionalRotation, true)
        return
      end if
      m.lastTextValue = m.text
      m.width = m.font.GetOneLineWidth(m.text, 10000)
      m.height = m.font.GetOneLineHeight() * BGE.getNumberOfLinesInAString(m.text)

      m.tempCanvas = CreateObject("roBitmap", {
        width: m.width,
        height: m.height,
        AlphaEnable: true
      })
      m.owner.game.canvas.renderer.DrawTextTo(m.tempCanvas, m.text, 0, 0, BGE.Colors().White, m.font, "left")
      m.drawRegionToCanvas(m.tempCanvas, additionalRotation, true)
      m.shouldRedraw = false
    end function


    override function getWorldPosition() as BGE.Math.Vector
      ' TODO - Fix this for rotated in Z axis
      position = m.owner.position.add(m.offset)'super.getWorldPosition()
      if m.alignment = BGE.UI.HorizAlignment.center
        position.x -= m.width / 2
      else if m.alignment = BGE.UI.HorizAlignment.right
        position.x -= m.height
      end if
      return position
    end function

  end class

end namespace
