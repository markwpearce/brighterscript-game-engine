namespace BGE.UI

  class UiContainer extends BGE.UI.UiWidget

    backgroundRGBA as integer = BGE.RGBAtoRGBA(128, 128, 128, 0.8) ' RGBA value for the background of the window/container
    showBackground as boolean = true ' Should the background be drawn?

    protected children as object = []

    sub new(game as BGE.Game)
      super(game)
    end sub


    ' Method for handling any actions needed when this is removed from view
    ' Clears all children
    override sub onRemove()
      m.clearChildren()
    end sub


    sub addChild(element as UiWidget)
      if element <> invalid
        m.children.push(element)
      end if
    end sub

    sub removeChild(element as UiWidget)
      indexToRemove = -1
      if invalid = element
        return
      end if

      for i = 0 to m.children.count() - 1
        if m.children[i].id = element.id
          indexToRemove = i
          exit for
        end if
      end for
      if indexToRemove >= 0 and invalid <> m.children[indexToRemove]
        m.children[indexToRemove].onDestroy()
        m.children.delete(indexToRemove)
      end if
    end sub

    sub clearChildren()
      for each element in m.children
        element.onDestroy()
      end for
      m.children.clear()
    end sub


    ' Method for handling any updates based on time since previous frame
    ' Calls same method in all children
    '
    ' @param {float} deltaTime - milliseconds since last frame
    override sub onUpdate(deltaTime as float)
      for each child in m.children
        if child <> invalid
          child.onUpdate(deltaTime)
        end if
      end for
    end sub

    ' Set the canvas this UIWidget Draws to
    ' Sets the canvas on all children
    '
    ' @param {object} [canvas=invalid] The canvas this should draw to - if invalid, then will draw to the game canvas
    override sub setCanvas(canvas = invalid as object)
      super.setCanvas(canvas)
      if invalid <> m.children
        for each child in m.children
          if child <> invalid
            child.setCanvas(m.canvas)
          end if
        end for
      end if
    end sub

    ' Method called each frame to draw any images of this entity
    '
    override sub draw()
      if m.showBackground
        m.canvas.DrawRect(m.position.x, m.position.y, m.width, m.height, m.backgroundRGBA)
      end if
      m.children.sortBy("zIndex")
      for each child in m.children
        if invalid <> child and child.enabled
          child.repositionBasedOnParent(m)
          child.draw()
        end if
      end for
    end sub


    ' Function to get the value of the UIContainer, which will be an object of all the values of the children
    '
    ' @return {object} - the value of this element
    override sub getValue() as object
      value = {}
      for each child in m.children
        value.addReplace(child.name, child.getValue())
      end for
      return value
    end sub



    ' Method to process input per frame
    ' Calls same method in all children
    '
    ' @param {BGE.GameInput} input - GameInput object for the last frame
    override sub onInput(input as BGE.GameInput)
      for each child in m.children
        child.onInput(input)
      end for
    end sub


    ' Method to process an ECP keyboard event
    ' @see  https://developer.roku.com/en-ca/docs/developer-program/debugging/external-control-api.md
    ' Calls same method in all children
    '
    ' @param {integer} char
    override sub onECPKeyboard(char as integer)
      for each child in m.children
        child.onECPKeyboard(char)
      end for
    end sub


    ' Method to process an External Control Protocol event
    ' @see  https://developer.roku.com/en-ca/docs/references/brightscript/events/roinputevent.md
    ' Calls same method in all children
    '
    ' @param {object} data
    override sub onECPInput(data as object)
      for each child in m.children
        child.onECPInput(data)
      end for
    end sub


    ' Method to handle audio events
    ' @see  https://developer.roku.com/en-ca/docs/references/brightscript/events/roaudioplayerevent.md
    ' Calls same method in all children
    '
    ' @param {object} msg - roAudioPlayerEvent
    override sub onAudioEvent(msg as object)
      for each child in m.children
        child.onAudioEvent(msg)
      end for
    end sub


    ' Called when the game pauses
    ' Calls same method in all children
    '
    override sub onPause()
      for each child in m.children
        child.onPause()
      end for
    end sub


    ' Called when the game unpauses
    ' Calls same method in all children
    '
    ' @param {integer} pauseTimeMs - The number of milliseconds the game was paused
    override sub onResume(pauseTimeMs as integer)
      for each child in m.children
        child.onResume(pauseTimeMs)
      end for
    end sub


    ' Called on url event
    ' @see  https://developer.roku.com/en-ca/docs/references/brightscript/events/rourlevent.md
    ' Calls same method in all children
    '
    ' @param {object} msg - roUrlEvent
    override sub onUrlEvent(msg as object)
      for each child in m.children
        child.onUrlEvent(msg)
      end for
    end sub


    ' General purpose event handler for in-game events.
    ' Calls same method in all children
    '
    ' @param {string} eventName - Event name that describes the event type
    ' @param {object} data - Any extra data to go along with the event
    override sub onGameEvent(eventName as string, data as object)
      for each child in m.children
        child.onGameEvent(eventName, data)
      end for
    end sub



    ' Method called when the current room changes.
    ' This method is only called when the entity is marked as `persistant`,
    ' otherwise entities are destroyed on room changes.
    ' Calls same method in all children
    '
    ' @param {Room} newRoom - The next room
    override sub onChangeRoom(newRoom as BGE.Room)
      for each child in m.children
        child.onChangeRoom(newRoom)
      end for
    end sub


    ' Method called when this entity is destroyed
    '
    override sub onDestroy()
      m.clearChildren()
    end sub

  end class
end namespace
