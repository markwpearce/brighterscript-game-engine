class MainRoom extends BGE.Room
  ball = invalid

  sub new(game as BGE.Game) as void
    super(game)
    m.name = "MainRoom"

  end sub

  override sub onUpdate(dt as float)
    if not BGE.isValidEntity(m.ball)
      m.ball = m.game.addEntity(new Ball(m.game), {direction: -1})
    end if
  end sub


  override sub onDrawBegin(renderer as BGE.Renderer)
    w = renderer.getCanvasSize().x
    h = renderer.getCanvasSize().y
    white = BGE.Colors().White
    red = BGE.Colors().Red
    grey = BGE.Colors().Grey
    renderer.DrawRectangle(0, 0, w, h, grey)
    renderer.DrawRectangleOutline(3, 3, w - 3, h - 3, red)

    renderer.DrawText("Canvas", w / 2, 100, white, m.game.getFont("default"), "center")
    renderer.DrawText("Move with arrows", w / 2, 240, white, m.game.getFont("default"), "center")
    renderer.DrawText("Scale with replay/options", w / 2, 270, white, m.game.getFont("default"), "center")
    renderer.DrawText("Center with play/pause", w / 2, 300, white, m.game.getFont("default"), "center")
  end sub

  override sub onInput(input as BGE.GameInput)
    canvasOffset = m.game.canvas.getOffset()
    canvasScale = m.game.canvas.getScale()

    delta = 2
    scaleDelta = 0.01

    if input.isButton("back")
      m.game.End()
    end if

    canvasOffset.x += delta * input.x
    canvasOffset.y += delta * input.y

    if input.isButton("options")
      canvasScale.x *= (1 + scaleDelta)
      canvasScale.y *= (1 + scaleDelta)
    else if input.isButton("replay")
      canvasScale.x *= (1 - scaleDelta)
      canvasScale.y *= (1 - scaleDelta)
    end if

    m.game.canvas.setOffset(canvasOffset.x, canvasOffset.y)
    m.game.canvas.setScale(canvasScale.x, canvasScale.y)

    if input.isButton("play")
      m.game.centerCanvasToScreen()
    end if

  end sub

end class
