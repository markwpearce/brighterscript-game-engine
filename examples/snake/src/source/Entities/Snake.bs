class Snake extends GridEntity

  segments = []

  timeCount = 0
  dead = false
  maxCells = 10


  xDirection = 0
  yDirection = 0

  ' slow game loop to 10 fps instead of 60
  secondsPerFrame = 0.1

  snakeHeadCollider = invalid

  sub new(game)
    super(game)
    m.name = "snake"
  end sub

  override sub onCreate(args)
    m.gridX = args.gridX
    m.gridY = args.gridY
    m.position.xDirection = 1
    m.position.yDirection = 0
    m.grid = args.grid

    m.snakeHeadCollider = m.addRectangleCollider("snake_head", m.grid, m.grid, 0, 0)
    m.snakeHeadCollider.enabled = false
  end sub



  override sub onCollision(colliderName as string, other_colliderName as string, other_entity as BGE.GameEntity)
    if other_entity.name = "apple"
      m.maxCells++
      m.game.postGameEvent("score")
      m.game.playSound("score", 50)
      m.game.destroyEntity(other_entity)
    end if

    if other_entity.name = "MainRoom"
      ?"Collision: " + colliderName + " and " + other_colliderName + " of " + other_entity.name
      m.die()
      return
    end if
  end sub

  override sub onUpdate(dt as float)
    if m.dead
      return
    end if

    m.timeCount += dt
    if m.timeCount < m.secondsPerFrame
      return
    end if
    m.timeCount = m.timecount - m.secondsPerFrame
    m.snakeHeadCollider.enabled = true

    m.gridX += m.position.xDirection
    m.gridY += m.position.yDirection

    m.refreshPosition()

    ' keep track of where snake has been. front of the array is always the head
    m.segments.unshift({x: m.gridX, y: m.gridY})

    ' remove cells as we move away from them
    if m.segments.count() > m.maxCells
      m.segments.pop()
    end if

    ' if the snake goes out of bounds
    if m.checkPositionForSnake(m.gridX, m.gridY, true)
      m.die()
      return
    end if
  end sub

  override sub onInput(input)
    if m.position.yDirection = 0 and (input.isButton("up") or input.isButton("down"))
      m.position.yDirection = input.y
      m.position.xDirection = 0
    else if m.position.xDirection = 0 and (input.isButton("left") or input.isButton("right"))
      m.position.xDirection = input.x
      m.position.yDirection = 0
    end if
  end sub



  override sub onDrawBegin(canvas)
    ' Draw Head
    m.drawRectangleOnGrid(canvas, m.gridX, m.gridY, BGE.Colors().Green)

    green = 255
    for each segment in m.segments
      green = 0.95 * green ' make snake get darker, just for fun
      if green < 128
        green = 128
      end if

      m.drawRectangleOnGrid(canvas, segment.x, segment.y, BGE.RGBAtoRGBA(0, green, 0))
    end for
  end sub


  function checkPositionForSnake(gridX, gridY, ignoreHead = false) as boolean
    i = 0
    for each segment in m.segments
      if gridX = segment.x and gridY = segment.y
        if not ignoreHead or i > 0
          return true
        end if
      end if
      i++
    end for
    return false
  end function

  sub die()
    m.dead = true
    m.snakeHeadCollider.enabled = false

    m.game.playSound("die", 75)
    m.game.postGameEvent("game_over")
  end sub

end class
