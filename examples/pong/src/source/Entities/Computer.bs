class Computer extends Paddle



  function new(game) as void
    super(game)
    m.name = "Computer"
    m.position.x = 1280 - 50
    m.position.y = invalid
  end function


  override function getFrontColliderXOffset() as float
    return -m.width / 2
  end function


  override function onUpdate(dt)
    ballEntity = m.game.getEntityByName("Ball")

    ' If there is a ball and the ball is moving to the right and hasn't gotten to the computer paddle yet move paddle towards ball
    if ballEntity <> invalid and ballEntity.velocity.x > 0 and ballEntity.position.x < m.position.x
      if ballEntity.position.y < m.position.y - 20
        if m.position.y > m.bounds.top + m.height / 2
          m.position.y -= 3.5 * 60 * dt
        else
          m.position.y = m.bounds.top + m.height / 2
        end if
      else if ballEntity.position.y > m.position.y + 20
        if m.position.y < m.bounds.bottom - m.height / 2
          m.position.y += 3.5 * 60 * dt
        else
          m.position.y = m.bounds.bottom - m.height / 2
        end if
      end if
    end if
  end function

end class
