sub Main()


  game = new BGE.Game(1280, 720, true) ' This initializes the game engine

  game.loadBitmap("roku", "pkg:/sprites/roku-logo-purple.png")

  cubes_room = new CubesRoom(game)
  game.defineRoom(cubes_room)

  trees_room = new TreesRoom(game)
  game.defineRoom(trees_room)

  images_room = new ImagesRoom(game)
  game.defineRoom(images_room)

  game.changeRoom(getRoomNames()[0])

  game.getDebugUI().addChild(new BGE.Debug.FpsDisplay(game))
  game.getDebugUI().addChild(new BGE.Debug.InputDisplay(game))

  game.getDebugUI().addChild(new BGE.Debug.MemoryDisplay(game))
  game.getDebugUI().addChild(new BGE.Debug.GarbageCollectorDisplay(game))
  game.debugDrawColliders(true)
  ' game.debugDrawSafeZones(true)
  game.debugShowUi(true)
  game.Play()

  ' vscode_rale_tracker_entry
end sub

function getRoomNames()
  return ["ImagesRoom", "TreesRoom", "CubesRoom"]
end function


sub goToNextRoom(currentRoom as BGE.Room, direction as integer)

  currentIndex = 0
  i = 0
  roomNames = getRoomNames()
  for each name in roomNames
    if currentRoom.name = name
      currentIndex = i
      exit for
    end if
    i++
  end for

  nextIndex = currentIndex + direction

  if nextIndex >= roomNames.count()
    nextIndex = 0
  else if nextIndex < 0
    nextIndex = roomNames.count() - 1
  end if

  currentRoom.game.changeRoom(roomNames[nextIndex])
end sub