class BaseRoom extends BGE.Room

  cameraSpeed = 100
  cameraVelocity = 0
  cameraRotation = 0

  debugEnabled = false
  instructions = ""

  sub new(game)
    super(game)
    m.name = "BaseRoom"
  end sub


  override sub onUpdate(dt)
    if m.cameraRotation <> 0
      m.game.canvas.renderer.camera.rotate(new BGE.Math.Vector(0, m.cameraRotation * dt, 0))
    end if
    if m.cameraVelocity <> 0
      m.game.canvas.renderer.camera.position.plusEquals(m.game.canvas.renderer.camera.orientation.scale(m.cameraSpeed * dt * m.cameraVelocity))
    end if
  end sub

  override sub onInput(input)
    m.handleCameraMovement(input)
    m.handleSceneChange(input)
    if input.press and input.isButton("replay")
      m.debugEnabled = not m.debugEnabled
      m.game.debugDrawEntityDetails(m.debugEnabled)
      m.game.debugShowUi(m.debugEnabled)
    end if
  end sub

  protected sub handleCameraMovement(input)
    m.cameraVelocity = 0
    m.cameraRotation = 0
    if input.x <> 0 or input .y <> 0
      m.cameraVelocity = input.y
      m.cameraRotation = input.x
    end if
  end sub

  override sub onDrawEnd(renderObj as BGE.Renderer)
    font = m.game.getFont("default")
    frameCenter = renderObj.getCanvasCenter()
    renderObj.DrawText(m.instructions, frameCenter.x, 100, BGE.Colors().white, font, "center")
    text = "Move Camera: Arrows | Change Scene: <</>> | Debug Information: Replay"
    renderObj.DrawText(text, frameCenter.x, 140, BGE.Colors().white, font, "center")
  end sub

  protected sub handleSceneChange(input)
    if input.press
      if input.isButton("fastforward")
        goToNextRoom(m, 1)
      else if input.isButton("rewind")
        goToNextRoom(m, -1)
      else if input.isButton("back")
        m.game.end()
      end if
    end if
  end sub

end class
