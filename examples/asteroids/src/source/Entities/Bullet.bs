class Bullet extends BGE.GameEntity

  speed = 100
  angle = 0
  bulletCollider = invalid

  sub new(game)
    super(game)
    m.name = "Bullet"
  end sub


  override sub onCreate(args)
    m.position = args.position
    m.rotation = args.rotation

    m.speed = args.speed
    m.velocity.x = m.speed * cos(m.rotation.z)
    m.velocity.y = m.speed * sin(m.rotation.z)

    m.width = 10
    m.height = 10
    m.bulletCollider = m.addCircleCollider("bullet", m.width / 2)
    m.game.playSound("laser", 50)
  end sub

  override sub onUpdate(deltaTime as float)
    m.checkOffScreen()
  end sub


  sub checkOffScreen()
    w = m.game.getCanvas().GetWidth()
    h = m.game.getCanvas().GetHeight()

    if m.position.x < 0 or m.position.x > w or m.position.y < 0 or m.position.y > h
      m.game.destroyEntity(m)
    end if
  end sub


  override sub onDrawEnd(renderObj as BGE.Renderer)
    drawPosition = renderObj.worldPointToCanvasPoint(m.position)
    renderObj.drawSquare(drawPosition.x, drawPosition.y, m.width, BGE.Colors().Green)
  end sub

end class
