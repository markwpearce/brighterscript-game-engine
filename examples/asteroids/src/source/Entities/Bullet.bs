class Bullet extends BGE.GameEntity

  speed = 100
  angle = 0
  bulletCollider = invalid

  function new(game) as void
    super(game)
    m.name = "Bullet"
  end function


  override function onCreate(args)
    m.position = args.position
    m.rotation = args.rotation

    m.speed = args.speed
    radRotation = BGE.Math.DegreesToRadians(m.rotation.z)
    m.velocity.x = m.speed * cos(radRotation)
    m.velocity.y = m.speed * sin(radRotation)

    m.width = 10
    m.height = 10
    m.bulletCollider = m.addCircleCollider("bullet", m.width / 2)
    m.game.playSound("laser", 50)
  end function


  override function onCollision(colliderName as string, other_colliderName as string, other_entity as object)
    ?"Collision: " + colliderName + " and " + other_colliderName + " of " + other_entity.name
  end function



  override function onUpdate(deltaTime as float)
    m.checkOffScreen()
  end function


  function checkOffScreen() as void
    w = m.game.getCanvas().GetWidth()
    h = m.game.getCanvas().GetHeight()

    if m.position.x < 0 or m.position.x > w or m.position.y < 0 or m.position.y > h
      m.game.destroyEntity(m)
    end if
  end function


  override function onDrawBegin(canvas as object)
    canvas.drawPoint(m.position.x, m.position.y, m.width / 2, BGE.Colors().Green)
  end function

end class
