class Explosion extends BGE.GameEntity

  explosion as BGE.Sprite
  totalLifeTime as float = 0
  soundName as string

  sub new(game)
    super(game)
    m.name = "Explosion"
  end sub


  override sub onCreate(args)
    m.position = args.position
    m.zIndex = 1
    m.rotation = args.rotation
    m.soundName = args.soundName
    m.explosion = m.addSprite("boom", m.game.getBitmap("boom"), 128, 128)
    m.explosion.scale = BGE.Math.createScaleVector(args.imageScale)
    m.explosion.applyPreTranslation(-64, -80)
    m.explosion.addAnimation("boom", {startFrame: 0, frameCount: 64}, 60, "forward")
    m.playExplosion()
  end sub

  override sub onUpdate(dt as float)
    m.totalLifeTime += dt
    if m.totalLifeTime >= 1
      m.invalidate()
    end if
  end sub

  private sub playExplosion()
    explosionName = m.soundName
    if invalid = explosionName
      explosionNum = Rnd(9)
      explosionName = "explosion0" + stri(explosionNum).trim()
    end if
    m.game.playSound(explosionName, 70)
    m.explosion.playAnimation("boom")
  end sub

end class
